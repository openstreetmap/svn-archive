<?xml version="1.0" encoding="utf-8"?>
<edit:SingleTagEditor
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:edit="net.systemeD.potlatch2.mapfeatures.editors.*"
	verticalGap="0"
	width="100%"
	toolTip="{fieldDescription}">

  <mx:Label text="{fieldName}:"/>
  <edit:ChoiceComboBox id="inputBox" dataProvider="{choices}" selectedItem="{selectFromTag}"
      width="100%" change="value = inputBox.selectedItem.value"
      open="inputBox.dropdown.variableRowHeight = true">
      <edit:itemRenderer>
        <mx:Component>
        <mx:HBox toolTip="{data.description}">
          <mx:Image source="{data.icon}"/>
          <mx:Label htmlText="{data.label}"/>
        </mx:HBox>
        </mx:Component>
      </edit:itemRenderer>
  </edit:ChoiceComboBox>

  <mx:Script><![CDATA[
      import mx.collections.*;
      import mx.utils.*;
      import net.systemeD.potlatch2.mapfeatures.Feature;
      
      private var _choices:ArrayCollection = null;
      private var _unknownChoice:Choice = null;

      [Bindable(event="factory_set")]
      protected function get choices():ArrayCollection {
          if ( _choices == null ) {
              _choices = new ArrayCollection();
              for each ( var choice:Choice in ChoiceEditorFactory(_factory).choices )
                  _choices.addItem(choice);
              _choices.addItem(createUnsetChoice());
          }
          return _choices;
      }
      
      [Bindable(event="tag_changed")]
      protected function get selectFromTag():Object {
          var tagValue:String = value;
          for each(var choice:Choice in choices) {
              if ( choice.isTagMatch(tagValue) )
                  return choice;
          }
          
          // set up the unknown choice
          if ( _unknownChoice == null ) {
              _unknownChoice = new Choice();
              _unknownChoice.description = "Unknown value";
              _choices.addItem(_unknownChoice);
          }
          _unknownChoice.label = "<i>Unknown:</i> "+Feature.htmlEscape(tagValue);
          _unknownChoice.icon = null;
          _unknownChoice.value = tagValue;
          
          return _unknownChoice;
      }
      
      private function createUnsetChoice():Choice {
          var choice:Choice = new Choice();
          choice.icon = null;
          choice.description = "Field not set";
          choice.label = "<i>Unset</i>";
          choice.value = null;
          return choice;
      }
  ]]></mx:Script>
</edit:SingleTagEditor>


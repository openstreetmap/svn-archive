#!/usr/bin/perl
#
# Author:  Petter Reinholdtsen
# Date:    2005-03-23
# License: GPL
#
# Parse a GPX file, and add lat/lon bounding information into the meta
# header.

use strict;
use warnings;
use XML::Parser;
use vars qw($minlat $maxlat $minlon $maxlon);

my $parser = new XML::Parser(Handlers => {Start => \&handle_start});

for my $gpxfile (@ARGV) {
    # Make sure to zero out the values for each file
    undef $minlat; undef $maxlat; undef $minlon; undef $maxlon;

    $parser->parsefile($gpxfile);
    insert_bounds($gpxfile);
}

sub update_minmax {
    my ($lat, $lon)  = @_;
    $minlat = $lat if ! defined $minlat;
    $maxlat = $lat if ! defined $maxlat;
    $minlon = $lon if ! defined $minlon;
    $maxlon = $lon if ! defined $maxlon;

    $minlat = $lat if $lat < $minlat;
    $maxlat = $lat if $lat > $maxlat;
    $minlon = $lon if $lon < $minlon;
    $maxlon = $lon if $lon > $maxlon;
}

sub handle_start {
    my ($expat, $element, %attr) = @_;
    if ("trkpt" eq $element || "wpt" eq $element) {
	update_minmax($attr{'lat'}, $attr{'lon'});
    }
}

#
# Insert the calculated bounds into the GPX file.  Should be rewritten
# to understand XML, instead of faking it by assuming the tags are on
# separate lines.
#
sub insert_bounds {
    my ($gpxfile) = @_;
    open(OLD, "< $gpxfile") or die "Unable to read from $gpxfile";
    open(NEW, "> $gpxfile.new") or die "Unable to write to $gpxfile.new";
    my $inserted = 0;
    while (<OLD>) {
	if (!$inserted && m%^(.*)<bounds [^>]*>(.*)$%) {
	    $inserted = 1;
	    $_ = "$1<bounds minlat=\"$minlat\" minlon=\"$minlon\" maxlat=\"$maxlat\" maxlon=\"$maxlon\"/>$2\n";
	}
	if (!$inserted && m%^(.*)(</metadata>.*)$%i) {
	    $inserted = 1;
	    $_ = "$1<bounds minlat=\"$minlat\" minlon=\"$minlon\" maxlat=\"$maxlat\" maxlon=\"$maxlon\"/>\n$2\n";
	}
	print NEW or die "Unable to write";
    }
    close(NEW) or die "Unable to write";
    close(OLD);
    rename "$gpxfile.new","$gpxfile";
}
